{
	parserClass="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptParser"
	extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
	psiClassPrefix="StellarisScript"
	psiImplClassSuffix="Impl"
	psiPackage="com.windea.plugin.idea.stellaris.script.psi"
	psiImplPackage="com.windea.plugin.idea.stellaris.script.psi.impl"
	elementTypeHolderClass="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptTypes"
	tokenTypeClass="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptTokenType"
	elementTypeClass="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptElementType"
	psiImplUtilClass="com.windea.plugin.idea.stellaris.script.psi.impl.StellarisScriptPsiImplUtil"

	tokens=[
	  EQUAL_SIGN="="
	  LT_SIGN="<"
	  GT_SIGN=">"
	  LE_SIGN="<="
	  GE_SIGN=">="
	  LEFT_BRACE="{"
	  RIGHT_BRACE="}"
	  EOL="regexp:\R"
	  BLANK="regexp:\s+"
	  COMMENT="regexp:#[^\r\n]*"
	  END_OF_LINE_COMMENT="regexp:#[^\r\n]*"
	  VARIABLE_NAME_ID="regexp:@[a-zA-Z0-9_]+"
	  VARIABLE_REFERENCE_ID="regexp:@[a-zA-Z0-9_]+"
	  PROPERTY_KEY_ID='regexp:[a-zA-Zz0-9_\-]+'
	  BOOLEAN_TOKEN='regexp:(yes)|(no)'
	  NUMBER_TOKEN='regexp:-?[0-9]+(\.[0-9]+)?'
	  UNQUOTED_STRING_TOKEN='regexp:[^@\s\[\]{}="][^\s\[\]{}="]*'
	  STRING_TOKEN='regexp:"([^"(\r\n\\]|\\.)*?"'
	  COLOR_TYPE="regexp:(rgb)|(rgba)|(hsv)"
	  COLOR_PARAMETER='regexp:\{[^\r\n]*?}'
	]
}

//忽略空白，在ParserDefinition里面定义PsiElement之间的空白类型
//regex必须定向到token，而非rule
//即使idea提示不需要转义，一些字符在regexp中仍然需要转义

//DONE 列表的元素可以是字符串
//DONE 分隔符可以是比较运算符

root ::= root_item *
private root_item ::= END_OF_LINE_COMMENT | COMMENT | variable_definition | property | item
variable_definition ::= variable_name variable_definition_separator variable_value {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.script.psi.impl.StellarisScriptNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptNamedElement"
  methods=[getName setName getNameIdentifier getTextOffset getIcon getValue]
}
variable_name ::= VARIABLE_NAME_ID
variable_definition_separator ::= "="
variable_value ::= number {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue]
}
variable_reference ::= VARIABLE_REFERENCE_ID {
  methods=[getReference]
}

property ::= property_key property_separator property_value {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.script.psi.impl.StellarisScriptNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.script.psi.StellarisScriptNamedElement"
  methods=[getName setName getNameIdentifier getIcon]
}
property_key ::= PROPERTY_KEY_ID
property_separator ::= "=" | "<" | ">" | "<=" | ">="
property_value ::= variable_reference | boolean | number | string | block
//property_value ::= boolean | number | variable_reference | string | array | object

block ::= "{" block_item * "}" {
  pin=1
  implements="com.intellij.psi.PsiListLikeElement"
  methods=[isEmpty isArray isObject getComponents]
}
private block_item ::= END_OF_LINE_COMMENT | COMMENT | property | item

item ::= boolean | number | string | color {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getIcon getValue]
}

boolean ::= BOOLEAN_TOKEN {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue]
}
number ::= NUMBER_TOKEN {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue]
}
string ::= UNQUOTED_STRING_TOKEN | STRING_TOKEN {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue getReference isValidPropertyKey]
}
color ::= COLOR_TYPE COLOR_PARAMETER {
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue getColor]
}
