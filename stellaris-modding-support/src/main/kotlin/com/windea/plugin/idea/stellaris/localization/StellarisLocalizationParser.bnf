{
	parserClass="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationParser"
	extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
	psiClassPrefix="StellarisLocalization"
	psiImplClassSuffix="Impl"
	psiPackage="com.windea.plugin.idea.stellaris.localization.psi"
	psiImplPackage="com.windea.plugin.idea.stellaris.localization.psi.impl"
	elementTypeHolderClass="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationTypes"
	tokenTypeClass="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationTokenType"
	elementTypeClass="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationElementType"
	psiImplUtilClass="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationPsiImplUtil"

	tokens=[
		COLON=":"
	  NUMBER="regexp:\d+"
	  BLANK="regexp:\s+"
	  COMMENT="regexp:#[^\r\n]*"
	  ROOT_COMMENT="regexp:#[^\r\n]*"
	  END_OF_LINE_COMMENT="regexp:#[^\r\n]*"
	  LOCALE_ID="regexp:[a-z_]+"
	  PROPERTY_KEY_ID="regexp:[a-zA-Z0-9_.\-']+"
	  VALID_ESCAPE_TOKEN='regexp:\\["rn$£§%\[]'
	  INVALID_ESCAPE_TOKEN='regexp:\\.'
	  LEFT_QUOTE="\""
	  RIGHT_QUOTE="\""
	  PROPERTY_REFERENCE_START="$"
	  PROPERTY_REFERENCE_SEPARATOR="|"
    PROPERTY_REFERENCE_PARAMETER="regexp:[^$\r\n]+"
	  PROPERTY_REFERENCE_END="$"
	  CODE_START="["
	  CODE_TEXT="regexp:[^\[\]\r\n]+"
	  CODE_END="]"
	  ICON_START="£"
	  ICON_ID="regexp:[a-zA-Z\-_\\/]+"
	  ICON_END="£"
	  SERIAL_NUMBER_START="%"
	  SERIAL_NUMBER_ID="regexp:[A-Z]"
	  SERIAL_NUMBER_END="%"
	  COLORFUL_TEXT_START="§"
	  COLOR_CODE="regexp:[a-zA-Z]"
	  COLORFUL_TEXT_END="§!"
	  //NOTE 实际上双引号不需要转义，但是这里认为是语法错误
	  //QUOTED_STRING_TOKEN='regexp:[^$£§\[\r\n\\][^"%$£§\[\r\n\\]*'
	  QUOTED_STRING_TOKEN='regexp:[^"$£§\[\r\n\\][^"%$£§\[\r\n\\]*'
	]
}

//规则定义可以忽略空白
//直接导向token的rule，在flex文件中需要使用对应的token
//即使idea提示不需要转义，一些字符在regexp中仍然需要转义

root ::= ROOT_COMMENT * [root_item]
private root_item ::= locale property_list *
locale ::= LOCALE_ID ":" {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
  methods=[getName setName getNameIdentifier getIcon getLocale getReference]
}
private property_list ::= END_OF_LINE_COMMENT | COMMENT | property
property ::= property_key ":" [NUMBER] property_value {
  pin=1
	mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
	implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
	methods=[getName setName getNameIdentifier getIcon getKey getValue  ]
}
property_key ::= PROPERTY_KEY_ID
property_value ::= LEFT_QUOTE rich_text * RIGHT_QUOTE{
  pin=1
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue]
}

rich_text ::= property_reference | code | icon | serial_number | colorful_text | escape | string

string ::= QUOTED_STRING_TOKEN
escape ::= VALID_ESCAPE_TOKEN | INVALID_ESCAPE_TOKEN
property_reference ::= PROPERTY_REFERENCE_START PROPERTY_KEY_ID [PROPERTY_REFERENCE_SEPARATOR [PROPERTY_REFERENCE_PARAMETER]] PROPERTY_REFERENCE_END {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
  methods=[getName setName getNameIdentifier getTextOffset getReference]
}
code ::= CODE_START CODE_TEXT? CODE_END{
  pin=1
}
icon ::= ICON_START icon_name ICON_END {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
  methods=[getName setName getNameIdentifier getTextOffset  getReference]
}
icon_name ::= property_reference | ICON_ID
serial_number ::= SERIAL_NUMBER_START SERIAL_NUMBER_ID SERIAL_NUMBER_END {
  pin=1
  mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
  methods=[getName setName getNameIdentifier getTextOffset getSerialNumber getReference]
}
colorful_text ::= COLORFUL_TEXT_START COLOR_CODE rich_text* [COLORFUL_TEXT_END]{
  pin=1
  mixin="com.windea.plugin.idea.stellaris.localization.psi.impl.StellarisLocalizationNamedElementImpl"
  implements="com.windea.plugin.idea.stellaris.localization.psi.StellarisLocalizationNamedElement"
  methods=[getName setName getNameIdentifier getTextOffset getColor getReference]
}
